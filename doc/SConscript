#
# Copyright 2011 Tanjeff-Nicolai Moos <tanjeff@cccmz.de>
#
# This file is part of the agentXcpp library.
#
# AgentXcpp is free software: you can redistribute it and/or modify
# it under the terms of the AgentXcpp library license, version 1, which 
# consists of the GNU General Public License and some additional 
# permissions.
#
# AgentXcpp is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# See the AgentXcpp library license in the LICENSE file of this package 
# for more details.
#



# This is a damn hack to get scons working with doxygen: We use the Command 
# Builder to define how to invoke doxygen. Now, if scons is invoked from the 
# parent directory, it will invoke doxygen in that directory, giving it e.g.  
# doc/api.doxyfile as parameter. Doxygen finds its config, but does not find 
# the sources (it searches the sources in ../src/, but they are actually in 
# src/).
# 
# So we hack the command accordingly, adding a "cd doc" to it and introducing a 
# lot of dependencies. If the direcory structure of the agentxcpp project is 
# ever changed, this scons script will likely break, and it will be a mess to 
# get it working again... I will care about this later. After all it works this 
# way.


# Get the environment from the SConscript above
Import('env')



# API documentation


# Build it
# Note: To find out whether a rebuild is neccessary,
#       Scons looks at the "target". If the target is
#       a directory, things don't work, thus we use
#       a HTML file as target
api = Command('api/html/index.html',
	      'api.doxyfile',
	      "cd doc/ && doxygen api.doxyfile > /dev/null")

# API doc depends on src/ directory
# Note: although not all files in there are actually
#       dependencies, we pretend it was so, to avoid forgotten
#       deps
Depends(api, Glob('../src/*.cpp') + Glob('../src/*.h'))

# API doc also depends on other files
Depends(api, Glob('*.dox'))

# Scons does not remove directories when invoked with -c,
# so we explicit request deletion of the api/ directory
# in those cases:
Clean(api, "api")

# Provide an install target
Install(env['docdir'], "api")




# The same for the internal documentation:

internals = Command('internals/html/index.html',
    'internals.doxyfile',
    "cd doc/ && doxygen internals.doxyfile > /dev/null")
Depends(internals, Glob('../src/*.cpp') + Glob('../src/*.h'))
Depends(internals, Glob('*.dox'))
Clean(internals, "internals")


# provide an alias for installing
Alias("install", env['docdir'] + "/api")


#
# Copyright 2011 Tanjeff-Nicolai Moos <tanjeff@cccmz.de>
#
# This file is part of the agentXcpp library.
#
# AgentXcpp is free software: you can redistribute it and/or modify
# it under the terms of the AgentXcpp library license, version 1, which 
# consists of the GNU General Public License and some additional 
# permissions.
#
# AgentXcpp is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# See the AgentXcpp library license in the LICENSE file of this package 
# for more details.
#


import os


# Build the API documentation

# Note: To find out whether a rebuild is neccessary,
#       Scons looks at the "target". If the target is
#       a directory, things don't work, thus we use
#       a HTML file as target
api = Command('api/html/index.html',
	      'api.doxyfile',
	      "doxygen $SOURCE > /dev/null")

# API doc depends on src/ directory
# Note: although not all files in there are actually
#       dependencies, we pretend it was so, to avoid forgotten
#       deps
Depends(api, Glob('../src/*'))

# API doc also depends on other files
Depends(api, Glob('*.dox'))

# Scons does not remove directories when invoked with -c,
# so we explicit request deletion of the api/ directory
# in those cases:
Clean(api, "api")


# The same for the internal documentation:

internals = Command('internals/html/index.html',
    'internals.doxyfile',
    "doxygen $SOURCE > /dev/null")
Depends(internals, Glob('../src/*'))
Depends(internals, Glob('*.dox'))
Clean(internals, "internals")


# Provide an install target

# Fetch prefix from ENV or use default:
if os.environ.has_key('PREFIX'):
    prefix = os.environ['PREFIX']
else:
    prefix = "/usr/local"

# Install API documentation
docdir = "#" + prefix + "/share/doc/agentxcpp"
Install(docdir, api)
Alias("install", docdir)
